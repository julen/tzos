
declare function local:getSynonyms($term as element(term)) {
    $term/../..//term[string()!=$term/string()]
};

for $term in collection($collection)//term
where $term[starts-with(lower-case(string()), $letter)] and $term/../..[@xml:lang=$lang]
return
<dl class="term">
    <dt class="term">{ $term/string() }</dt>
    <dd>

    { (: If any, display synonyms :)
      let $syns := local:getSynonyms($term)
      return
      if (exists($syns)) then
        <dl class="syn">
            <dt class="in"><abbr class="small" title="{{{{ _('Synonyms') }}}}">{{{{ _('syn.') }}}}</abbr></dt>
            {
            for $syn at $i in $syns
            let $synCount := count($syns)
            return
            <dd class="in">{ $syn/string() }{ if ($i=$synCount) then () else ", "}</dd>
            }
        </dl>
      else ()
    }

        <dl class="trans">
        {
        for $trans in $term/../../..//term
        let $transLang := data($trans/../../@xml:lang)
        where $trans/../..[@xml:lang!=$lang]
        return
        (
            <dt>{ $transLang }</dt>,
            <dd lang="{ $transLang }">{ $trans/string() }</dd>
        )
        }
        </dl>
    </dd>
</dl>
